#+title: GNU Emacs Config
#+author: Petar Đurašković
#+description: Petar Đurašković personal GNU Emacs configuration.
#+startup: fold
#+options: toc:2
#+auto_tangle: t
#+PROPERTY: header-args :tangle "./config.el"

* Table of contents :toc:
- [[#important-things-to-load-first][Important things to load first]]
  - [[#env][Env]]
  - [[#straight-package-manager][Straight Package Manager]]
  - [[#theme][Theme]]
- [[#browser][Browser]]
  - [[#eww-search-engine][Eww search engine]]
- [[#custom-functions][Custom Functions]]
  - [[#cooking-timer][Cooking timer]]
  - [[#yank-n-times][Yank N times]]
- [[#consult][Consult]]
- [[#dired][Dired]]
- [[#emms][EMMS]]
- [[#email-mu4e][Email (mu4e)]]
- [[#fonts][Fonts]]
  - [[#setting-the-font-face][Setting The Font Face]]
- [[#settings-for-programming][Settings for programming]]
  - [[#electric-pair-mod][Electric pair mod]]
  - [[#rust-settings][Rust settings]]
  - [[#general-compiling-settings][General compiling settings]]
- [[#graphical-user-interface-tweaks][Graphical User Interface tweaks]]
  - [[#disable-menubar-toolbars-and-scrollbars][Disable Menubar, Toolbars and Scrollbars]]
  - [[#disable-font-lock-mode][Disable font-lock-mode]]
  - [[#display-line-numbers-and-truncated-lines][Display line numbers and truncated lines]]
- [[#orderless][Orderless]]
- [[#org-mode][Org Mode]]
  - [[#auto-tangle-org-file][Auto tangle org file]]
  - [[#enabling-table-of-contents][Enabling Table of Contents]]
  - [[#enabling-org-bullets][Enabling Org bullets]]
  - [[#disable-electric-indent][Disable electric indent]]
  - [[#enable-org-tempo][Enable org tempo]]
  - [[#disable-code-block-highlighting][Disable code block highlighting]]
  - [[#setting-header-font-size][Setting header font size]]
  - [[#hooks][Hooks]]
- [[#rss-elfeel][RSS (elfeel)]]
- [[#pacmacs][Pacmacs]]
- [[#prog-mode][Prog-mode]]
- [[#pdf-tools][Pdf-tools]]
- [[#vertico][Vertico]]
- [[#virtual-machines][Virtual machines]]
- [[#vterm][Vterm]]
- [[#which-key][Which key]]
- [[#wttrin][Wttrin]]
- [[#keybindings][Keybindings]]
  - [[#ibuffer][Ibuffer]]
  - [[#config][Config]]
  - [[#compiling][Compiling]]
  - [[#elfeed][Elfeed]]
  - [[#emms-1][Emms]]
  - [[#yank-n-times-1][Yank N times]]
  - [[#vterm-1][Vterm]]

* Important things to load first
** Env
#+begin_src emacs-lisp
(setenv "LC_COLLATE" "C")
(setq x-hyper-keysym 'Hyper_L)
#+end_src
** Straight Package Manager
#+begin_src emacs-lisp
(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name
        "straight/repos/straight.el/bootstrap.el"
        (or (bound-and-true-p straight-base-dir)
            user-emacs-directory)))
      (bootstrap-version 7))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

(straight-use-package 'use-package)
(straight-use-package 'org)
#+end_src

** Theme
#+begin_src emacs-lisp
(load-theme 'tango-dark t)
#+end_src
* Browser
** Eww search engine
#+begin_src emacs-lisp
(setq eww-search-prefix "http://127.0.0.1:8888/search?q=")
#+end_src

* Custom Functions
** Cooking timer
#+begin_src emacs-lisp

(defvar zeko/cooking-timer-process nil "Timer process for the cooking timer.")
(defvar zeko/cooking-timer-remaining-time -1 "Remaining time in seconds for cooking timer.")
(defvar zeko/cooking-timer-sound "~/.emacs.d/sounds/sabaton.mp3" "Sound that's being played when timer is finished.")

(defun zeko/cooking-timer-countdown ()
  "Function that updates echo area with remaining time."
  (setq zeko/cooking-timer-remaining-time (1- zeko/cooking-timer-remaining-time))
  (let ((minutes (floor zeko/cooking-timer-remaining-time 60))
        (seconds (% zeko/cooking-timer-remaining-time 60)))
    (message "Timer: %02d:%02d left" minutes seconds)
    (when (eq zeko/cooking-timer-remaining-time 0)
        (message "Timer: Done!")
        (emms-play-file zeko/cooking-timer-sound)
        (cancel-timer zeko/cooking-timer-process)
        (setq zeko/cooking-timer-process nil)
        (setq zeko/cooking-timer-remaining-time -1))))

(defun zeko/cooking-timer-cancel ()
  "Cancel the running cooking timer."
  (interactive)
  (if zeko/cooking-timer-process
      (progn
        (cancel-timer zeko/cooking-timer-process)
        (setq zeko/cooking-timer-process nil)
        (setq zeko/cooking-timer-remaining-time -1)
        (message "Timer canceled..."))
    (message "No timer running...")))

(defun zeko/cooking-timer-start ()
  (interactive)
  "Play a sound after period of time in minutes"
  (let ((number (read-number "Time in minutes: ")))
    (if zeko/cooking-timer-process
        (message "Timer is already running, cancel it first!")
      (progn
        (setq zeko/cooking-timer-remaining-time (* number 60))
        (setq zeko/cooking-timer-process
              (run-at-time 0 1 #'zeko/cooking-timer-countdown))))))

#+end_src
** Yank N times
#+begin_src emacs-lisp
(defun zeko/yank-n-times (n)
  "Yank (paste) the top of the kill buffer N times."
  (interactive "p")
  (dotimes (_ n)
    (yank)
    (newline)))
#+end_src
* Consult
#+begin_src emacs-lisp
(use-package consult
  :straight t
  :bind (
         ("C-s" . consult-line)
	   ("C-r" . consult-line))
  :custom
  (consult-preview-key 'any) 
  (consult-async-min-input 2))
#+end_src

* Dired
#+begin_src emacs-lisp
  (use-package dired-preview :straight t)
  (add-hook 'dired-mode-hook (lambda () (dired-preview-mode 1)))
  (setq dired-listing-switches "-lha --time-style=+%d/%m/%Y")
#+end_src

* EMMS
#+begin_src emacs-lisp
  (use-package emms
    :straight t
    :commands (zeko/emms-playlist-new-named emms-smart-browse emms-start)
    :init
    (defun zeko/capitalize-words (str)
      "Capitalize each word in the input string STR, and make the rest of the string lowercase."
      (mapconcat
       (lambda (word)
         (concat (upcase (substring word 0 1))
                 (downcase (substring word 1))))
       (split-string str)
       " "))

    :config
    (require 'emms-setup)
    (require 'emms-playlist-mode)
    (emms-standard)
    (emms-default-players)
    
    (setq emms-cache-file "~/.emacs.d/emms/cache")
    (setq emms-info-asynchronously t)

    (defun zeko/emms-playlist-new-named (name)
      "Create and switch to new EMMS playlist named NAME."
      (interactive "sEnter playlist name: ")
      (let* ((capitalized-name (zeko/capitalize-words name))
             (formatted-name (format "*EMMS: %s*" capitalized-name))
             (buf (emms-playlist-new formatted-name))
             (dir (expand-file-name (concat "~/Music/" capitalized-name))))

        (unless (file-directory-p dir)
          (kill-buffer buf)
          (cl-return-from zeko/emms-playlist-new-named (format "Directory `%s` not found!" dir)))

        (switch-to-buffer buf)
        (emms-playlist-set-playlist-buffer buf)
        (emms-add-directory dir))))
#+end_src

* Email (mu4e)
#+begin_src emacs-lisp
  (setq user-full-name "Petar Đurašković")
  (setq user-mail-address "developmentpetar@gmail.com")

  (use-package mu4e
    :ensure nil
    ;; As on Arch you have to have load-path
    :load-path "/usr/share/emacs/site-lisp/mu4e/"
    :config
    (setq mu4e-change-filenames-when-moving t)
    (setq mu4e-update-interval (* 10 60))
    (setq mu4e-maildir "~/.emacs.d/Mail")

    (setq mu4e-drafts-folder  "/[Gmail].Drafts")
    (setq mu4e-sent-folder    "/[Gmail].Sent Mail")
    (setq mu4e-refile-folder  "/[Gmail].All Mail")
    (setq mu4e-trash-folder   "/[Gmail].Trash")

    (setq message-send-mail-function 'smtpmail-send-it
      	smtpmail-smtp-server "smtp.gmail.com"
      	smtpmail-smtp-service 587
      	smtpmail-stream-type 'starttls)

    (setq auth-sources '(password-store))

    (setq mu4e-maildir-shortcuts
          '(("/Inbox"               . ?i)
            ("[Gmail].Sent Mail"    . ?s)
            ("[Gmail].Trash"        . ?t)
            ("[Gmail].Drafts Mail"  . ?d)
            ("[Gmail].All Mail"     . ?a))))
#+end_src

* Fonts
Defining the various fonts that Emacs will use

** Setting The Font Face
#+begin_src emacs-lisp
  (set-face-attribute 'default nil
  		    :font "JetBrains Mono"
   		    :height 120
   		    :weight 'medium)

  (set-fontset-font t 'unicode
                  (font-spec :family "Noto Color Emoji"))

  ;; These make commented text and keywords italic.
  (set-face-attribute 'font-lock-comment-face nil
  		    :slant 'italic)

  (set-face-attribute 'font-lock-keyword-face nil
  		    :slant 'italic)

  ;; Uncomment the following line if spacing needs adjusting
  (setq-default line-spacing 0.12)
#+end_src

* Settings for programming
** Electric pair mod
This is one thing that I really missed. Auto closing parenthesis, but there is a catch.
Here I have made it to perform auto close only in elisp mode, because in other languages
it annoys me. However, the thing that works globally is marking a region and just typing closing
parenthesis or quotes at the end and it will surround that area with it.

#+begin_src emacs-lisp
(electric-pair-mode 1)

;; Disable auto-pairing globally by default using the trick above
(setq-default electric-pair-inhibit-predicate
              (lambda (char) (not (use-region-p))))

;; Re enable full auto pairing for elisp mode
(add-hook 'emacs-lisp-mode-hook
          (lambda () 
            (setq-local electric-pair-inhibit-predicate #'electric-pair-default-inhibit)))
#+end_src
** Rust settings
It's necessary to install rust-mode because Emacs doesn't have it out of the box yet.
#+begin_src emacs-lisp
(use-package rust-mode 
  :straight t
  :mode ("\\.rs\\'" . rust-mode)
  :hook (rust-mode . (lambda () (face-remap-add-relative 'default :height 140))))
#+end_src

#+begin_src emacs-lisp
(add-hook 'conf-toml-mode-hook (lambda () (display-line-numbers-mode)))
#+end_src

** General compiling settings
#+begin_src emacs-lisp
(defvar zeko/mode-list
  '((rust-mode . "cargo run")
    (c-mode    . "make"))
  "Mapping major programming modes to compile commands.")

(defun zeko/language-options (option)
  "Provide build/compile/test option and switch to that buffer."
  (when option
    (compile option)
    (select-window (get-buffer-window "*compilation*"))))

(defun zeko/compile-for-major-mode ()
  "Get the compile command for the current major mode and run it."
  (interactive)
  (if-let ((command (alist-get major-mode zeko/mode-list)))
    (progn
	    (compile command)
      ;; Small delay to get the window appear, then select it
      (select-window (get-buffer-window "*compilation*")))
    (message "No compile command defined for %s" major-mode)))
#+end_src

* Graphical User Interface tweaks
Make GNU Emacs look a bit better...

** Disable Menubar, Toolbars and Scrollbars
#+begin_src emacs-lisp
  ;; Will uncomment later...
  ;;(menu-bar-mode -1)
  ;;(tool-bar-mode -1)
  (scroll-bar-mode -1)
#+end_src

** Disable font-lock-mode
#+begin_src emacs-lisp
  (defvar zeko/font-lock-whitelist
    '(org-mode
      dired-mode
      ibuffer-mode
      vterm-mode)
    "List of modes that have font-lock-mode enabled.")

  ;; Filter out font-lock-mode easily
  (defun zeko/font-lock-conditional-enable ()
    "Enable font-lock-mode only in specific modes."
    (if (memq major-mode zeko/font-lock-whitelist)
        (font-lock-mode 1)
      (font-lock-mode -1)))

  (define-globalized-minor-mode zeko/global-font-lock-mode 
    font-lock-mode 
    zeko/font-lock-conditional-enable)

  (zeko/global-font-lock-mode 1)
#+end_src

** Display line numbers and truncated lines
#+begin_src emacs-lisp
(setq display-line-numbers-type 'relative)
(global-visual-line-mode t)
(add-hook 'text-mode-hook #'display-line-numbers-mode)
#+end_src

* Orderless
#+begin_src emacs-lisp
(use-package orderless
  :straight t
  :custom
  (completion-styles '(orderless basic))
  (completion-category-defaults nil)
  (completion-category-overrides '((file (styles partial-completion)))))
#+end_src
* Org Mode
There is one caviat with this function. It's stupid simple and it doesn't do well when document
scales. I will have to do some sort of hashing to improve snappiness and performance. 
** Auto tangle org file
#+begin_src emacs-lisp
(defun zeko/org-babel-tangle-on-save ()
  "Tangle the current buffer if #+auto_tangle: t is present."
  (when (and (eq major-mode 'org-mode)
	         'buffer-modified-p
             (save-excursion
               (goto-char (point-min))
               (search-forward "#+auto_tangle: t" nil t)))
    (let ((org-confirm-babel-evaluate nil))
      (org-babel-tangle))))

(add-hook 'after-save-hook #'zeko/org-babel-tangle-on-save)
#+end_src

** Enabling Table of Contents
#+begin_src emacs-lisp
  (use-package toc-org
    :straight t
    :commands toc-org-enable
    :init (add-hook 'org-mode-hook 'toc-org-enable))
#+end_src

** Enabling Org bullets
Org-bullets gives us pretty bullet points rather than asterisks.
#+begin_src emacs-lisp
(add-hook 'org-mode-hook 'org-indent-mode)
(use-package org-bullets :straight t)
(add-hook 'org-mode-hook (lambda () (org-bullets-mode 1)))
#+end_src

** Disable electric indent
Org mode source blocks have some really weird and annoying default indentation behavior. I think this has to do with
electric-indent-mode, which is turned on by default in Emacs. Turn this damn thing OFF!

#+begin_src emacs-lisp
(electric-indent-mode -1)
#+end_src
** Enable org tempo
This build in program, that's not on by default, enables us to make blocks with typing '<' + some block key + TAB.
e.g. 's' for source code '<s'.

Default emacs key is C-c C-,
#+begin_src emacs-lisp
(require 'org-tempo)
#+end_src
** Disable code block highlighting
#+begin_src emacs-lisp
(setq org-src-fontify-natively nil)
#+end_src
** Setting header font size
#+begin_src emacs-lisp
(defun zeko/org-faces ()
    (set-face-attribute 'org-todo nil :height 0.9)
    (set-face-attribute 'org-document-title nil :height 2.0 :foreground "white")
    (set-face-attribute 'org-level-1 nil :height 1.7)
    (set-face-attribute 'org-level-2 nil :height 1.6)
    (set-face-attribute 'org-level-3 nil :height 1.5)
    (set-face-attribute 'org-level-4 nil :height 1.4)
    (set-face-attribute 'org-level-5 nil :height 1.3)
    (set-face-attribute 'org-level-6 nil :height 1.2)
    (set-face-attribute 'org-level-7 nil :height 1.1)
    (set-face-attribute 'org-level-8 nil :height 1.0))
#+end_src
** Hooks
#+begin_src emacs-lisp
(add-hook 'org-mode-hook (lambda () (zeko/org-faces)  			   
			   (face-remap-add-relative 'default :height 130)))
#+end_src

* RSS (elfeel)
#+begin_src emacs-lisp
(use-package elfeed :straight t)

(use-package elfeed-org
  :straight t
  :after elfeed
  :config
  (setq rmh-elfeed-org-files '("~/org/elfeed.org"))
  (elfeed-org))
        
(defun zeko/elfeed-show-eww-if-tag (entry tag)
  "Browse elfeed ENTRY in eww if it is tagged with TAG."
  (when (member tag (elfeed-entry-tags entry))
    (let ((browse-url-browser-function #'eww-browse-url))
      (elfeed-show-visit))))

(defun zeko/elfeed-show-eww-if-tag-is-browse (entry)
  "Browse elfeed ENTRY in eww if it has the tag `browse'."
  (zeko/elfeed-show-eww-if-tag entry 'browse))

(defun zeko/elfeed-tag-buzzwords (entry)
  "Remove buzzwords and topics from elfeed."
  (let ((title (elfeed-entry-title entry)))
    (when (string-match-p
 	   (rx (or "ai" "llm" "openai" "ai-generated" "chatgpt"))
	   (downcase title))
      (elfeed-tag entry 'buzzword))))

(setq-default elfeed-search-filter "@3-months-ago -buzzword")
(advice-add #'elfeed-show-entry :after #'zeko/elfeed-show-eww-if-tag-is-browse)
(add-hook 'elfeed-new-entry-hook #'zeko/elfeed-tag-buzzwords)
#+end_src

* Pacmacs
#+begin_src emacs-lisp
  (use-package pacmacs 
    :straight t
    :hook (pacmacs-mode . (lambda () 
  			  (setq line-spacing 0))))
#+end_src
* Prog-mode
#+begin_src emacs-lisp
  (defun zeko/prog-mode-setup ()
    "My custom settings for all programming modes."
    (display-line-numbers-mode 1))

  (add-hook 'prog-mode-hook #'zeko/prog-mode-setup)
#+end_src
* Pdf-tools
#+begin_src emacs-lisp
(use-package pdf-tools
  :straight t
  :mode ("\\.pdf\\'" . pdf-view-mode)
  :hook (pdf-view-mode . pdf-isearch-minor-mode))
#+end_src
* Vertico
Make this thing perform auto completion only on TAB. Popups are annoying.
vertico-flat extensions potentially solves this.

#+begin_src emacs-lisp
(use-package vertico
  :straight t
  :init
  (vertico-mode))

(custom-set-faces '(vertico-current ((t (:background "#2e3436" :inherit bold)))))
#+end_src
* Virtual machines
#+begin_src emacs-lisp
(defun zeko/run-temple-vm ()
  "Start the QEMU VM using a fragmented list of arguments."
  (interactive)
  (let* ((binary "qemu-system-x86_64")
         (flags '("-enable-kvm"
                  "-m 2G"
                  "-drive file=$HOME/qemu/temple,format=qcow2"
                  "-machine pcspk-audiodev=snd0"
                  "-audiodev pa,id=snd0"
                  "-rtc base=localtime"
                  "-vga std"))
         (full-command (mapconcat 'identity (cons binary flags) " ")))
    (shell-command (concat full-command " &"))))

(define-prefix-command 'zeko/virtual-machine)
(keymap-set global-map "H-v" 'zeko/virtual-machine)

(keymap-set zeko/virtual-machine "r t" #'zeko/run-temple-vm)
#+end_src

* Vterm
#+begin_src emacs-lisp
(use-package vterm :straight t)
#+end_src
* Which key
#+begin_src emacs-lisp
  (use-package which-key
    :straight t
    :init (which-key-mode 1)
    :config
    (setq which-key-side-window-location 'bottom
	which-key-sort-order #'which-key-key-order-alpha
	which-key-sort-uppercase-first t
	which-key-add-column-padding 1
	which-key-max-display-columns nil
	which-key-min-display-lines 6
	which-key-side-window-slot -10
	which-key-side-window-max-height 0.25
	which-key-idle-delay 0.8
	which-key-max-description-length 25
	which-key-allow-impercise-window-fit t
	which-key-separator " → "))
#+end_src
* Wttrin
#+begin_src emacs-lisp  
(use-package wttrin :straight t)
(keymap-set global-map "C-c w w" (lambda () (interactive) (wttrin "Podgorica")))
#+end_src

* Keybindings
** Ibuffer
#+begin_src emacs-lisp
  (defun zeko/ibuffer-toggle ()
    "Toggle ibuffer buffer."
    (interactive)
    (if-let ((buf (get-buffer "*Ibuffer*")))
        (switch-to-buffer buf)
      (ibuffer)))

  (keymap-set global-map "<f9>" #'zeko/ibuffer-toggle)

  (with-eval-after-load 'ibuffer
    (define-prefix-command 'zeko/ibuffer)
    (keymap-set global-map "C-c i" 'zeko/ibuffer)

    (keymap-set 'zeko/ibuffer "g" #'ibuffer-toggle-filter-group)
    (keymap-set 'zeko/ibuffer "r" #'ibuffer-filter-disable)
    (keymap-set 'zeko/ibuffer "a" #'ibuffer-switch-to-saved-filter-groups))
#+end_src

** Config
#+begin_src emacs-lisp
  (defun zeko/open-config ()
    "Open my main Emacs config."
    (interactive)
    (find-file (expand-file-name "~/.emacs.d/config.org")))

  (defun zeko/reload-init-file ()
    "Reload Emacs init file."
    (interactive)
    (load-file user-init-file))

  (define-prefix-command 'zeko/config)
  (keymap-set global-map "C-c e" 'zeko/config)

  (keymap-set 'zeko/config "c" #'zeko/open-config)
  (keymap-set 'zeko/config "r" #'zeko/reload-init-file)
#+end_src

** Compiling
#+begin_src emacs-lisp
(keymap-set global-map "H-c" #'zeko/compile-for-major-mode)

;; Rust-specific "Run Options"
(with-eval-after-load 'rust-mode
  (keymap-set rust-mode-map "H-x r" (lambda () (interactive) (zeko/language-options "cargo build --release")))
  (keymap-set rust-mode-map "H-x t" (lambda () (interactive) (zeko/language-options "cargo test"))))
#+end_src

** Elfeed
#+begin_src emacs-lisp
(define-prefix-command 'zeko/elfeed)
(keymap-set global-map "C-c f" 'zeko/elfeed)

(keymap-set 'zeko/elfeed "o" #'elfeed)
(keymap-set 'zeko/elfeed "u" #'elfeed-update)
#+end_src
** Emms
#+begin_src emacs-lisp
(define-prefix-command 'zeko/emms)
(keymap-set global-map "C-c m" 'zeko/emms)

(keymap-set 'zeko/emms "d" #'zeko/emms-playlist-new-named)
(keymap-set 'zeko/emms "n" #'emms-next)
(keymap-set 'zeko/emms "m" #'emms-shuffle)
(keymap-set 'zeko/emms "l" #'emms-previous)
(keymap-set 'zeko/emms "q" #'emms-stop)
(keymap-set 'zeko/emms "p" #'emms-pause)
(keymap-set 'zeko/emms "f" #'emms-play-file)
#+end_src
** Yank N times
#+begin_src emacs-lisp
(keymap-set global-map "H-y" #'zeko/yank-n-times)
#+end_src

** Vterm
#+begin_src emacs-lisp
(define-prefix-command 'zeko/vterm)
(keymap-set zeko/vterm "c" #'vterm-send-C-c)

(keymap-set global-map "C-c t" #'vterm)

(with-eval-after-load 'vterm
  (keymap-set vterm-mode-map "C-c t" 'zeko/vterm))
#+end_src
